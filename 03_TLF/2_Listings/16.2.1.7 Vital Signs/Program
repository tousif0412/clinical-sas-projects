/*******************************************************************
* Client:  xxxxxx                                                          
* Project:  yyyyyyy                                                   
* Program: lis7.SAS  
*
* Program Type: Listing
*
* Purpose: To produce 16.2.1.7 Vital Signs 
* Usage Notes: 
*
* SASï¿½ Version: 9.2
* Operating System: Windows 2011
* Author: Tousif Tamboli
* Date Created: 14-JUN-2025
* Modification History:
*******************************************************************/ 
         
/* 1. GET ADAM DATASET INTO SAS */     
libname adam'/home/u63981529/CL/adam';

/* 2. KEEP REQUIRED VARIABLES AS PER MOCK SHELL*/

DATA ADSL;
   SET ADAM.ADVS;
   if adtm ne . then adtc=strip(put(adtm,e8601dt.));
   else adtc=strip(put(adt,e8601da.));
   AVISIT=strip(AVISIT);
   KEEP USUBJID PARAMN PARAM AVISITN AVISIT  AVALC adtc TRT01A; 
RUN;

/* 3. Apply manual pagination logic (20 records per page) */
data adsl;
   set adsl;
   retain lnt 0 page1 1;
   lnt+1;

   if lnt>20 then do;
      page1=page1+1;
      lnt=1;
   end;
run;

/* 4. Include RTF style macro */
%include "/home/u63981529/CL/TLF/macros/RTFSTYLE.sas";
%_RTFSTYLE_;

/* 5. Assign titles and footnotes */

title1 j=l 'COVID-19 AA';
title2 j=l 'Protocol: 043-1810';
title3 j=c '16.2.1.7 Vital Signs ';

footnote1 j=l '/home/u63981529/CL/Program/tlf/LIS7.SAS';

options orientation=landscape;
ods escapechar='^';
ods rtf file='//home/u63981529/CL/TLF/output/Listing/L_16_2_1_7.rtf' style=styles.test;

proc report data=adsl split='|' style= {outputwidth=100%};

   column page1 USUBJID PARAMN PARAM AVISITN AVISIT  AVALC adtc TRT01A;

   define page1/order noprint;

   define usubjid/order "Subject"
   style (column)={just=l cellwidth=15%}
   style (header)={just=l cellwidth=15%}
   ;

   define PARAMN/ order noprint; /* couple variables */
   define PARAM/ "Parameter (unit)"
   style (column)={just=l cellwidth=20%}
   style (header)={just=l cellwidth=20%}
   ;

   define AVISITN/ order noprint; /* couple variables */
   define AVISIT/ "Visit"
   style (column)={just=l cellwidth=19%}
   style (header)={just=l cellwidth=19%}
   ;

   define AVALC/ "Observed value"
   style (column)={just=l cellwidth=12%}
   style (header)={just=l cellwidth=12%}
   ;

   define adtc/ "Date/Time of|Measurements"
   style (column)={just=l cellwidth=19%}
   style (header)={just=l cellwidth=19%}
   ;
   define TRT01A/ "Treatment"
   style (column)={just=l cellwidth=13%}
   style (header)={just=l cellwidth=13%}
   ;
   compute before _page_;
      line@1 "^{style [outputwidth=100% bordertopwidth=0.5pt]}";
   endcomp;

   compute after _page_;
      line@1 "^{style [outputwidth=100% bordertopwidth=0.5pt]}";
   endcomp;

   break after page1/page;
run;

ods _all_ close;
