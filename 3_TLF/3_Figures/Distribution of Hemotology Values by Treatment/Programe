/* Step 1 — Create a custom ODS style */
proc template;
   define style styles.symbols;
   parent=styles.statistical;
      style graphdata1 /
          markersymbol='circlefilled'
          contrastcolor=green;
      style graphdata2 /
          markersymbol='circlefilled'
          contrastcolor=red;
   end;
run;

/* Step 2 — Assign ADAM library */

libname adam '/home/u63981529/CL/adam';

/* Step 3 — Prepare Hematology lab data */

data lb1;
set adam.adlb;
label value ="Analysis value";
if parcat1 eq 'HEMATOLOGY' and trt01a ne '';
test=paramcd;
drug=trt01a;
value=aval;
keep test drug value;
run;

/*Step 4 — Set Titles */
TITLE1 J=L "COVID-19 AA";
TITLE2 J=L "Protocol: 043";
title3 j=c "Figure 16.1.1  Distribution of Hemotology Values by Treatment";

/* Step 5 — Open ODS RTF output */

options orientation=landscape;
ods escapechar='^';
ods rtf file ="/home/u63981529/CL/Program/tlf/output/16_1_1.rtf" style=styles.symbols;

/* Step 6 — Create a grouped VBOX plot */

proc sgplot data=lb1;
   vbox value / category=test group=drug;
   xaxis label="Treatment";
   keylegend / title="Drug Type" ;
run; 

ods _all_ close; 
ods listing;
