/* STEP 1: Read ADSL and Prepare Basic Variables */

libname adam"/home/u63981529/CL/adam";					/* Points to the ADaM library. */
run;

data adsl01;
    set adam.adsl;
    where SAFFL="Y";
    if sex ="F" then asex="Female";
    else if sex="M" then asex="Male";
    arace=propcase(race);
run;
 
/* STEP 2: Create TOTAL Column */

data adsl02;
    set adsl01;
 
    length treatmentc $100;
 
    treatment=trt01pn;						/* First output = actual treatment group */
    treatmentc=trt01p;
    output;
 
    treatment=4;							/*  Second output = Total column */
    treatmentc="Total";
    output;
run;
 
/* STEP 3: Get Treatment Totals (N values) */
 
data dummy_trttotals;									/* Create dummy treatment rows */
    do treatment=1,2,3,4;
        output;
    end;
run;

 
proc freq data=adsl02;									/* Calculate actual N using PROC FREQ */
    tables  treatment  /out=trttotals01(rename=(count=trttotal) drop=percent);
run;
 
 
data trttotals;											/* Merge dummy + actual totals */
    merge dummy_trttotals(in=a) trttotals01(in=b);
    by treatment;
    if a and not b then trttotal=0;
run;
 
/* Create macro variables (N=&trt1, &trt2...) */	/* Used later in report headers: Dose level 1 (N=xx) */
 
data _null_;
    set trttotals;
    call symputx(cats("trt",treatment),trttotal);
run;
 
/* STEP 4: Create Utility Structure for Sex and Race*/
 
data adsl03_numeric;
    set adsl02;
    length grouplabel $200;
 
    group=1;
    grouplabel="Age (years)";
    result=age;
    output;
 
    group=3;
    grouplabel="Height (cm)";
    result=BHGHTSI;
    output;
run;
 
/* Create a format for treatment levels - using proc format */

proc format;
    value treatment
    1=1
    2=2
    3=3
    4=4
    ;
run;
 
/* STEP 5: Calculate Descriptive Statistics (PROC SUMMARY) */

proc sort data=adsl03_numeric out=adsl04_numeric;
    by group grouplabel;
run;
 
proc summary data=adsl04_numeric nway completetypes;
    by group grouplabel;
    class treatment/preloadfmt;
    var  result;
    output out=stats01 n= nmiss= mean= std= min= q1= median= q3= max= /autoname;
    format treatment treatment.;
run;
 
/* STEP 6: Format Statistics for Display */

data stats02;
    set stats01;
    length mean std q1 q3 min max nnmiss meanstd median q1q3 minmax $30;
 
    *process individual statistics;
 
 
    if result_mean ne . then mean=put(result_mean,5.1);
 
    if result_stddev ne . then std=" ("||put(result_stddev,6.2)||")";
    else std=" ( - )";
 
    if result_median ne . then median=put(result_median,5.1);
    if result_q1 ne . then q1=put(result_q1,5.1);
    if result_q3 ne . then q3=put(result_q3,5.1);
    if result_min ne . then min=put(result_min,3.);
    if result_max ne . then max=put(result_max,3.);
 
    *create combined statistics;
    nnmiss=put(result_n,3.)||" ("||put(result_nmiss,3.)||")";
    if result_n ne 0 then do;
        meanstd=trim(mean)||trim(std);
        q1q3=trim(q1)||","||trim(q3);
        minmax=trim(min)||","||trim(max);
    end;
run;
 
/* STEP 7: Keep Required Variables Only */

data stats03;
    set stats02;
    keep group grouplabel treatment nnmiss meanstd q1q3 median minmax;
run;
 
/* STEP 8: Transpose Statistics into Rows */

proc sort data=stats03;
    by group grouplabel treatment;
run;
 
proc transpose data=stats03 out=stats04;
    by group grouplabel treatment;
    var nnmiss meanstd median q1q3 minmax;
run;
 
/* STEP 9: Create Ordering & Labels */

data stats05;
    set stats04;
 
    _name_=upcase(_name_);
 
    length statistic $100;
 
    if _name_="NNMISS"  then do; intord=1; statistic="n (missing)"; end;
    if _name_="MEANSTD" then do; intord=2; statistic="Mean (SD)";   end;
    if _name_="MEDIAN"  then do; intord=3; statistic="Median";      end;
    if _name_="Q1Q3"    then do; intord=4; statistic="Q1, Q3"; end;
    if _name_="MINMAX"  then do; intord=5; statistic="Min, Max"; end;
run;
 
/* STEP 10: Transpose Treatments into Columns */

 
proc sort data=stats05;
    by group grouplabel intord statistic;
run;
 
proc transpose data=stats05 out=stats06 prefix=trt;
    by group grouplabel intord statistic;
    var col1;
    id treatment;
run;
 
/* STEP 11: Create Final Dataset */
 
data final_numeric;
    set stats06;
    keep group grouplabel intord statistic trt1 trt2 trt3 trt4;
run;
 
/*=========================================================
Processing for categorical variables
=========================================================*/

/* STEP 12: Create Utility Structure for Sex and Race*/
 
data adsl03_categorical;
    set adsl02;
 
 
    length statistic $100;
 
    group=2;										/* Sex block Group 2 → Sex */
 
    if not missing(asex) then statistic=asex;
    else statistic="Missing";
    output;
 
    call missing(statistic);
 
    group=4;										/* Race block Group 4 → Race */
    if not missing(arace) then statistic=arace;
    else statistic="Missing";
 
    output;
run;
 
/* STEP 13: Get Counts (n) for Each Category */

proc freq data=adsl03_categorical noprint;
    tables group*treatment*statistic/out=counts01(drop=percent);
run;
 
/* STEP 14 : Create Dummy Rows (Ensure 0 Counts Appear) */

data dummy01;
    length grouplabel statistic $100;
 
    group=2; grouplabel="Sex, n(%)";
 
    intord=1;  statistic="Male";    output;
    intord=2;  statistic="Female";  output;
    intord=99; statistic="Missing"; output;
 
    group=4; grouplabel="Race, n(%)";
 
    intord=1;  statistic="Asian ";                     output;
    intord=2;  statistic="Black Or African American";  output;
    intord=3;  statistic="White";                      output;
    intord=4;  statistic="Not reported";               output;
    intord=99; statistic="Missing";                    output;
run;
 
/*----------------------------------------------------------
Create a row for each treament for record present in dummy01 - using a do loop with output statement
----------------------------------------------------------*/
data dummy02;
    set dummy01;
    do treatment=1,2,3,4;
        output;
    end;
run;
 
/* STEP 15: Merge Dummy Counts with Actual Counts */
 
proc sort data=dummy02;
    by group treatment statistic;
run;
 
proc sort data=counts01;
    by group treatment statistic;
run;
 
data counts02;
    merge dummy02(in=a) counts01(in=b);
    by group treatment statistic;
    if a and not b then count=0;
run;
 
/* STEP 16: Calculate Percentages */

/*  Sort treatment totals and counts */


proc sort data=trttotals;
    by treatment;
run;
 
proc sort data=counts02;
    by treatment;
run;
 
data counts03;
    merge counts02(in=a) trttotals(in=b);
    by treatment;
run;
 
/* STEP 17: Create percentage variable and concatenate count and percentage into a single variable */
 
data counts04;
    set counts03;
 
    if trttotal ne 0 then percent=count/trttotal*100;
 
    length cp $30;
 
    if count ne 0 then do;
        if percent ne 100 then cp=put(count,3.)||" ("||strip(put(percent,5.1))||")";
        else cp=put(count,3.)||" ("||strip(put(percent,3.))||")";
    end;
    else do;
       cp=put(count,3.);
    end;
run;
 
/* STEP 18: Restructure the dataset such that treatments appear as columns - using proc transpose */
 
proc sort data=counts04;
    by group grouplabel intord statistic;
run;
 
proc transpose data=counts04 out=counts05 prefix=trt;
    by group grouplabel intord statistic;
    var cp;
    id treatment;
run;
 
/* STEP 19: Create final dataset - keeping only required variables */

 
data final_categorical;
    set counts05;
    keep group grouplabel intord statistic trt1 trt2 trt3 trt4;
run;
 
 
/* STEP 20: Combine categorical and numeric variable summaries */
 
data final;
    set final_numeric final_categorical;
run;
 
proc sort data=final;
    by group grouplabel intord;
run;
 
 
/* STEP 21: Report generation */

%rtf_output_style_setup;
 
footnote "&outputname.";
title "Demographic characteristics - age, sex, race, height";
title2 "Full Analysis Set";
title3 bcolor="#e6f9ec" "Copyright @ www.mycsg.in";
footnote2 j=l "Percentages are calculated based on the number of subjects in each treatment.";
ods listing close;
ods rtf file="&outputpath.\&outputname." style=csgpool01;
 
 
proc report data = final center headline headskip nowd split='~' missing style(report)=[just=center]
   style(header)=[just=center];
 
   column group grouplabel intord statistic trt1 trt2 trt3 trt4 ;
 
   define group/order noprint;
   define intord /order noprint;
 
   define grouplabel/width=30 "Variable" order style(column)=[cellwidth=1.2in protectspecialchars=off] style(header)=[just=left];
   define statistic/width=30 "Statistic" style(column)=[cellwidth=1.5in protectspecialchars=off] style(header)=[just=left];
 
   define trt1/"Dose level 1" "(N=&trt1.)"   style(column)=[cellwidth=1.2in just=center] ;
   define trt2/"Dose level 2" "(N=&trt2.)"  style(column)=[cellwidth=1.2in just=center]   ;
   define trt3/"Dose level 3" "(N=&trt3.)"  style(column)=[cellwidth=1.2in just=center]   ;
   define trt4/"Total"        "(N=&trt4.)"  style(column)=[cellwidth=1.2in just=center]   ;
 
   compute after group;
     line @1 "";
   endcomp;
run;
 
ods rtf close;
ods listing;
