/* 1. Assign a library reference */
libname adam'/home/u63981529/CL/adam';

/* 2. Create a filtered ADSL dataset */

data adsl;
   set adam.adsl;
   keep usubjid RFICDTC ;
run;

/* 3. Apply manual pagination logic (20 records per page) */
data adsl;
   set adsl;
   retain lnt 0 page1 1;
   lnt+1;

   if lnt>20 then do;
      page1=page1+1;
      lnt=1;
   end;
run;

/* 4. Define report titles and footnotes */

title1 j=l "COVID-19 AA";
title2 j=l "Protocol: 043";
title3 j=c "16.2.1.2 Informed Consent";

footnote1 j=l "/home/u63925560/CL/Program/tlf/LIS2.SAS";
footnote2 j=r "Page ^{thispage} of ^{lastpage}";

/* 5. Generate the report using PROC REPORT */

proc report data=adsl nowd split='|' style={outputwidth=100%};
column page1 usubjid rficdtc;

/* Pagination */
define page1 / order noprint;

/* Subject */
define usubjid / order "Subject Number" 
                 style(column)={just=l cellwidth=30%}  
                 style(header)={just=l};

/* Informed Consent Date */
define rficdtc / order "Date of Informed Consent" 
	            style(column)={just=l cellwidth=30%}
                style(header)={just=l};

/* Top border for each page */
compute before _page_;
line @1 "^{style [outputwidth=100% bordertopwidth=0.5pt]}";
endcomp;

/* Bottom border for each page */
compute after _page_;
line @1 "^{style [outputwidth=100% bordertopwidth=0.5pt]}";
endcomp;

break after page1 / page;

run;
